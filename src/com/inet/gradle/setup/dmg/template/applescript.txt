try
    tell application "Finder"
        
        #Prepare centering on screen
        set screenSize to bounds of window of desktop
        set screenWidth to item 3 of screenSize
        set screenHeight to item 4 of screenSize

        set windowWidth to {{windowWidth}}
        set windowHeight to {{windowHeight}}
        set iconSize to {{iconSize}}

        tell disk "{{displayName}}"

            log "## Starting to set options of the DiskImage {{displayName}}"

            open

            set current view of container window to icon view
            set toolbar visible of container window to false
            set statusbar visible of container window to false

            # Center on Screen
            set bounds of container window to {(screenWidth - windowWidth) / 2.0, (screenHeight - windowHeight) / 2.0, (screenWidth + windowWidth) / 2.0, (screenHeight - windowHeight) / 2.0 + windowHeight}

            set theViewOptions to the icon view options of container window
            set arrangement of theViewOptions to not arranged

            set icon size of theViewOptions to iconSize

            try
                set text size of theViewOptions to {{fontSize}}
            on error errText
                log "Error setting 'theViewOptions' with error (will continue): " & errText
            end try

            set ourXPos to (windowWidth - 2.0 * iconSize) / 3.0 + iconSize / 3.0
            set appXPos to windowWidth - ourXPos
            set yPos to (windowHeight - iconSize) / 3.0 * 2.0

            try
                set position of item "{{executable}}.app" of container window to {ourXPos, yPos}
                make new alias file at container window to POSIX file "/Applications" with properties {name:"Applications"}
                set position of item "Applications" of container window to {appXPos, yPos}
            on error errText
                log "Error setting 'Applications Link' with error (will continue): " & errText
            end try

            try
                set ourXPos to windowWidth / 2.0
                set position of item "{{executable}}.pkg" of container window to {ourXPos, yPos}
            on error errText
                log "Error setting 'executable postion' with error (will continue): " & errText
            end try

            try
                set background color of theViewOptions to {{backgroundColor}}
            on error errText
                log "Error setting 'background color' with error (will continue): " & errText
            end try

            try
                set background picture of theViewOptions to file ".resources:background{{backgroundExt}}"
            on error errText
                log "Error setting 'background picture' with error (will continue): " & errText
            end try

            update without registering applications

            close

            open

            close

            log "## Done with the DiskImage {{displayName}}"
        end tell
    end tell
end try
# Leave newLine at the end 
